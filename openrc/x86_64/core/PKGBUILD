# Maintainer: artoo <artoo@artixlinux.org>
# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Contributor: williamh <williamh@gentoo.org>

_ever=1.1
_eurl="https://gitea.artixlinux.org/artix/openrc-extra/archive"
_aver=1.0
_aurl="https://gitea.artixlinux.org/artix/alpm-hooks/archive"

pkgbase=openrc
pkgname=('openrc' 'openrc-bash-completions' 'openrc-zsh-completions' 'openrc-deptree2dot')
pkgver=0.44.10
pkgrel=1
pkgdesc="Gentoo's universal init system"
arch=('x86_64')
url="https://github.com/OpenRC/openrc"
license=('BSD2')
makedepends=('psmisc' 'netifrc' 'zsh' 'perl' 'meson')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${pkgbase}-extra-${_ever}.tar.gz::${_eurl}/${_ever}.tar.gz"
        "alpm-hooks-${_aver}.tar.gz::${_aurl}/${_aver}.tar.gz"
        'openrc.logrotate'
        'sysctl.conf'
        'artix-rc-conf.patch'
        'artix-meson.patch')
sha256sums=('b6292d8d1bcd93469dbf0e30276dc5333ab36b4987a9a43aeb8ffc8539a7ace2'
            '36d5aaaab23f4ce9cdd8a0b558396b0a7f7469bc68987ffdaf9aad10ead85cba'
            '2bbbf26ed681420e7d2d4cde591786fed193dc38f43a6c5ab628f2cadc4f9191'
            '0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
            '874e50bd217fef3a2e3d0a18eb316b9b3ddb109b93f3cbf45407170c5bec1d6d'
            '8a256f18c25e67452e91b97f11b5eb2bb513f0014be069a6e8ba50e369f4d9b5'
            'ef6e4b8ccdfd79f9098e3e6d48ca26e10fac24d74982729fa83baff96bb4effb')

prepare(){
    cd "${pkgbase}-${pkgver}"
    patch -Np 1 -i ../artix-rc-conf.patch
    patch -Np 1 -i ../artix-meson.patch
}

check(){
     meson test -C build --print-errorlogs
}

build(){
    local meson_options=(
        -Dbranding="\"Artix Linux\""
        -Dos=Linux
        -Drootprefix=/usr
        -Dshell=/bin/bash
        -Dpam=true
        -Dsysvinit=true
        -Dpkgconfig=true
        -Dtermcap=ncurses
        -Dbash-completions=true
        -Dzsh-completions=true
        -Dsplit-usr=true
        -Dnewnet=false
        -Daudit=disabled
        -Dselinux=disabled
        -Dlibrcdir=openrc
    )

    artix-meson "${pkgbase}-${pkgver}" build "${meson_options[@]}"

    meson compile -C build
}

package_openrc() {
    depends=('psmisc' 'netifrc' 'inetutils')
    optdepends=('networkmanager-openrc: networkmanager init script'
                'elogind-openrc: elogind init script'
                'openrc-deptree2dot: Graph the Dependency Tree')
    backup=('etc/rc.conf'
            'etc/conf.d/consolefont'
            'etc/conf.d/keymaps'
            'etc/conf.d/hostname'
            'etc/conf.d/modules'
            'etc/conf.d/hwclock'
            'etc/conf.d/opentmpfiles-dev'
            'etc/conf.d/opentmpfiles-setup'
            'etc/conf.d/udev'
            'etc/conf.d/udev-trigger'
            'etc/conf.d/udev-settle'
            etc/conf.d/agetty.tty{1,2,3,4,5,6})
    provides=('svc-manager' 'init-rc' 'librc.so' 'libeinfo.so')
    conflicts=('svc-manager' 'init-rc')

    meson install -C build --destdir="${pkgdir}"

    install -Dm644 "${srcdir}/${pkgbase}".logrotate "${pkgdir}"/etc/logrotate.d/"${pkgbase}"

    install -d "${pkgdir}"/usr/lib/{openrc/cache,binfmt.d,sysctl.d}

    # sysctl defaults
    install -m755 "${srcdir}"/sysctl.conf "${pkgdir}"/usr/lib/sysctl.d/50-default.conf

    # license
    install -Dm644 ${pkgbase}-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgbase}"/LICENSE

    # openrc extra; agetty,kmod,udev,tmpfiles,sysusers
    cd "${srcdir}/${pkgbase}-extra"
    make DESTDIR="${pkgdir}" install

    # pacman hooks
    cd ${srcdir}/alpm-hooks
    make DESTDIR="${pkgdir}" install_openrc

    # split bash & zsh completions
    install -d "${srcdir}"/{_bash,_zsh}
    mv -v "${pkgdir}"/usr/share/bash-completion "${srcdir}"/_bash
    mv -v "${pkgdir}"/usr/share/zsh "${srcdir}"/_zsh

    # remove suport dir
    rm -r "${pkgdir}"/usr/share/openrc

    # remove init symlink
    rm -v "${pkgdir}"/usr/bin/init
}

package_openrc-bash-completions() {
    pkgdesc="OpenRC completions for Bash"

    install -d "${pkgdir}"/usr/share
    mv -v "${srcdir}"/_bash/* "${pkgdir}"/usr/share
}

package_openrc-zsh-completions() {
    pkgdesc="OpenRC completions for ZSH"

    install -d "${pkgdir}"/usr/share
    mv -v "${srcdir}"/_zsh/* "${pkgdir}"/usr/share
}

package_openrc-deptree2dot() {
    pkgdesc="Graph the OpenRC Dependency Tree"
    depends=('perl')

    cd "${pkgbase}-${pkgver}"
    install -Dm755 support/deptree2dot/deptree2dot "${pkgdir}"/usr/bin/deptree2dot
}

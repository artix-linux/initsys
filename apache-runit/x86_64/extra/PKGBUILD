# Maintainer: Muhammad Herdiansyah <koni@artixlinux.org>

_sed_args=(-e 's|/var/service|/run/runit/service|g' -e 's|/var/run|/run|g' -e 's|/usr/sbin|/usr/bin|g' -e 's|/opt/bin|/usr/bin|g' -e 's|/usr/libexec|/usr/lib|g')

pkgname=apache-runit
pkgver=20180605
pkgrel=3
pkgdesc="runit service scripts for apache"
arch=('any')
url="https://artixlinux.org"
license=('BSD')
groups=('runit-world')
depends=('apache' 'runit')
conflicts=('init-apache')
provides=('init-apache')
source=("apache.run"
        "apache.finish")
sha256sums=('6404e115334b559c6d8e91b63a7712d381c165639a252f4de3efee260f724a2e'
            '3c1bc2734b684c5af29871240a1532729731f7eab80171870e81b60e4ee37247')

_inst_logsv() {
    for file in run finish check; do
        if test -f "$srcdir/log$1.$file"; then
            install -Dm755 "$srcdir/log$1.$file" "$pkgdir/etc/runit/sv/$1/log/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/log/$file"
        fi
    done
}

_inst_sv() {
    if test -f "$srcdir/$1.conf"; then
        install -Dm644 "$srcdir/$1.conf" "$pkgdir/etc/runit/sv/$1/conf"
    fi

    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'apache'
}

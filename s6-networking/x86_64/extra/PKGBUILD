# Maintainer: Dudemanguy <dudemanguy@artixlinux.org>
# Contributor: Jacob Moody <moody@posixcafe.org>
pkgname=s6-networking
pkgver=2.5.1.1
pkgrel=2
pkgdesc="A suite of small networking tools for UNIX systems."
arch=('x86_64')
url="https://skarnet.org/software/s6-networking/"
license=('ISC')
depends=('s6' 's6-dns' 'bearssl' 'libs6.so' 'libs6dns.so')
provides=('libs6net.so')
source=("https://skarnet.org/software/s6-networking/${pkgname}-${pkgver}.tar.gz")
sha256sums=('7ac79d4de3d9c13532e444ab7497c4e04ad0fa7229d502984d9dc7d48aa64418')

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr \
                --datadir=/etc \
                --enable-ssl=bearssl \
                --enable-shared \
                --disable-static
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
} 

# Maintainer: Muhammad Herdiansyah <koni@artixlinux.org>

_sed_args=(-e 's|/var/run|/run|g' -e 's|\(/usr\)\?/sbin|/usr/bin|g')

pkgname=wpa_supplicant-runit
pkgver=20180429
pkgrel=3
pkgdesc="runit service scripts for wpa_supplicant"
arch=('any')
url="https://artixlinux.org"
license=('BSD')
groups=('runit-system')
depends=('wpa_supplicant' 'runit')
provides=('init-wpa_supplicant')
conflicts=('init-wpa_supplicant')
source=("wpa_supplicant.run")
sha256sums=('52ad4a194dadd82b3be85d8c09c7bf682983ebbc9b6b85663b81ed49ebd3160f')

_inst_sv(){
    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'wpa_supplicant'
}

# Maintainer: Muhammad Herdiansyah <koni@artixlinux.org>

_sed_args=(-e 's|/var/run|/run|g' -e 's|/usr/sbin|/usr/bin|g' -e 's|/opt/bin|/usr/bin|g' -e 's|/var/service|/run/runit/service|g' -e 's|/usr/libexec|/usr/lib|g')

pkgname=libvirt-runit
pkgver=20180801
pkgrel=3
pkgdesc="Runit service script for libvirt"
arch=('any')
url="https://artixlinux.org"
license=('BSD')
depends=('libvirt' 'runit')
groups=('runit-galaxy')
provides=('init-libvirt')
conflicts=('init-libvirt')
source=("libvirtd.run"
        "loglibvirtd.run"
        "virtlogd.run"
        "virtlockd.run")
sha256sums=('a7af0448bf41cebba5270fc43ef904ae47831310f1252957e49603591f2564b4'
            'd361bf15b6bb61cbe74cee03390a6801bf853b31529263ae7f9dc31cecadbbfb'
            '78fbe43281029837694ea94e058c66711288ad1d32fbf17526c341135ed6c5b1'
            '4231f344d345983513de37e9785f06b239afd58b09aa1be819b43928c7a76167')

_inst_logsv() {
    for file in run finish check; do
        if test -f "$srcdir/log$1.$file"; then
            install -Dm755 "$srcdir/log$1.$file" "$pkgdir/etc/runit/sv/$1/log/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/log/$file"
        fi
    done
}

_inst_sv() {
    if test -f "$srcdir/$1.conf"; then
        install -Dm644 "$srcdir/$1.conf" "$pkgdir/etc/runit/sv/$1/conf"
    fi

    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'libvirtd'
    _inst_logsv 'libvirtd'
    _inst_sv 'virtlogd'
    _inst_sv 'virtlockd'
}

# Maintainer: Dudemanguy <dudemanguy@artixlinux.org>
pkgname=s6-linux-utils
pkgver=2.5.1.7
pkgrel=1
pkgdesc='Tiny Linux-specific utilities.'
arch=('x86_64')
url='https://skarnet.org/software/s6-linux-utils/'
license=('ISC')
depends=('skalibs')
source=("https://skarnet.org/software/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('86a8c2df4bf13e17fb04b86b60dd2ab355f47bc4bf3c3821637cf9f599addfd8')
build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr \
                --datadir=/etc \
                --libexecdir=/usr/lib \
                --enable-shared
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
} 
